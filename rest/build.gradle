apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 5
        versionName "1.2.1"
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    implementation 'be.stannieman:commonservices:1.0.2.0'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
}

task createJavaDoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task createJavaDocJar(type: Jar, dependsOn: createJavaDoc) {
    classifier = 'javadoc'
    from createJavaDoc.destinationDir
}

task createSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'be.stannieman'
            artifactId 'rest'
            version '1.2.1'
            artifact "$buildDir\\outputs\\aar\\${project.getName()}-release.aar"
            artifact createJavaDocJar
            artifact createSourcesJar

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }

    repositories{
        maven {
            url "$buildDir\\repo"
        }
    }
}