apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0.0"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'be.stannieman:commonservices:1.0.0.3'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.6'
}

task createJavaDoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task createJavaDocJar(type: Jar, dependsOn: createJavaDoc) {
    classifier = 'javadoc'
    from createJavaDoc.destinationDir
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'be.stannieman'
            artifactId 'rest'
            version '1.0.0.0'
            artifact "$buildDir\\outputs\\aar\\signed-${project.getName()}-release.aar"
            artifact createJavaDocJar

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }

    repositories{
        maven {
            url "$buildDir\\repo"
        }
    }
}